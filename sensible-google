#!/usr/bin/perl

# Copyright © 2005, 2006, 2007 Jakub Wilk <jwilk@jwilk.net>
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the “Software”), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED “AS IS”, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

use strict;
use warnings;

use I18N::Langinfo qw(CODESET langinfo);
use Encode ();
use URI::Escape qw(uri_escape);

my %config_variables;
my $last_engine;
my %engines;
open CONFIG, '/etc/sensible-google' or die;
while (<CONFIG>)
{
  chomp;
  if (/^\$([a-z]+)=(.*)$/)
  {
    $config_variables{$1} = $2;
    next;
  }
  if (/^-([A-Za-z0-9]+|-)\t+(.*)$/)
  {
    my $code = $1;
    $last_engine = $2;
    $last_engine =~ s/\$\{([a-z]+)\}/$config_variables{$1}/ge;
    $engines{$code} = $last_engine;
    next;
  }
  if (/^\t+-([A-Za-z0-9]+)\t+(.*)$/)
  {
    my $code = $1;
    my $subengine = $2;
    $subengine =~ s/\$\{([a-z]+)\}/$config_variables{$1}/ge;
    $_ = $last_engine;
    s/%\?/$subengine/;
    $engines{$code} = $_;
    next;
  }
}
close CONFIG;

sub usage()
{
  print "Usage:\n    $0 --help\n    $0 [engine] keywords...\n\nEngines:\n";
  foreach my $option (sort keys %engines)
  {
    my $url = "http://" . $engines{$option};
    $url =~ s/%\?/<keywords>/;
    if ($option eq '-')
    {
      print " (none) $url\n";
    }
    else
    {
      printf "%7s %s\n", "-$option", $url
    }
  }
  exit 1;
}

my $search = undef;

my $engine = $engines{'-'};
PARAM:
while ($_ = shift @ARGV)
{
  $_ = qq/"$_"/ if index($_, ' ') >= 0 && index($_, '"') < 0;
  $search = "$search $_", next if defined $search;
  if ($_ eq '--help')
  {
    usage();
  }
  foreach my $code (keys %engines)
  {
    $engine = $engines{$code},
    next PARAM if $_ eq "-$code";
  }
  $search = shift(@ARGV) || '', next if $_ eq '--';
  $search = $_;
}

my $codeset = langinfo(CODESET);
$search .= '';
Encode::from_to($search, $codeset, 'UTF-8');
$search = uri_escape($search);
$engine =~ s/%\?/$search/;
exec '/usr/bin/sensible-browser', "http://$engine";

# vim:ts=2 sts=2 sw=2 et
